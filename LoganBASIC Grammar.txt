program  ->  statement* EOF ;

statement  ->  printStmt
             | assignment ;

printStmt   ->  "print" expression terminator ;
assignment  ->  IDENTIFIER "=" expression terminator ;
inputStmt   ->  expression "," IDENTIFIER terminator ;

expression  ->  equality ;
equality    ->  comparison ( ( "=" | "<>" ) comparison )* ;
comparison  ->  term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term        ->  factor ( ( "-" | "+" ) factor )* ;
factor      ->  unary ( ( "/" | "*" ) unary )* ;
unary       ->  ( "-" | "not" ) unary | primary ;
primary     ->  "true" | "false" | NUMBER_LIT | STRING_LIT | IDENTIFIER
              | "(" expression ")" ;
               
terminator  ->  ( "\n" | EOF ) ;

NUMBER_LIT  ->  DIGIT+ ( "." DIGIT+ )? ;
STRING_LIT  ->  "\"" <any char except "\"">* "\"" ;
IDENTIFIER  ->  ALPHA ( ALPHA | DIGIT )* ;
ALPHA       ->  "a" ... "z" | "A" ... "Z" | "_" ;
DIGIT       ->  "0" ... "9" ;